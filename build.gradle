plugins {
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id "org.asciidoctor.jvm.gems" version "3.3.2"
    id "org.asciidoctor.jvm.pdf" version "3.3.2"
}

wrapper {
    gradleVersion = "7.4.2"
}

repositories {
    mavenCentral()
    ruby.gems()
}

dependencies {
    asciidoctorGems 'rubygems:asciidoctor-bibtex:0.8.0'
    // asciidoctor-bibtex depends on version 1.x of citeproc-ruby and csl-styles,
    // but for some reason the build installs version 2.x by default.
    // To fix this we need these manual dependencies on the 1.x versions.
    asciidoctorGems 'rubygems:citeproc-ruby:1.1.14'
    asciidoctorGems 'rubygems:csl-styles:1.0.1.11'
}

task bibtex() {
    def inputDir = file("src/docs/bibtex")
    def combinedFile = file("src/docs/asciidoc/combined.bib")
    inputs.dir(inputDir)
    outputs.file(combinedFile)

    doFirst {
        combinedFile.text = fileTree(inputDir) {
            include "**/*.bib"
        }.collect {it.text}.join("\n")
    }
}

asciidoctor {
    dependsOn = [bibtex, asciidoctorGemsPrepare]

    outputOptions {
        backends = ['html5', 'pdf']
    }

    asciidoctorj {
        modules {
            diagram.use()
        }
        requires = ['asciidoctor-bibtex']
    }

    baseDirFollowsSourceDir()

    sources {
        include '*.adoc'
    }

    resources {
        from('src/docs/asciidoc') {
            include '**/*.png'
        }
    }
}

clean {
    delete('src/docs/asciidoc/combined.bib')
}

defaultTasks 'asciidoctor'
